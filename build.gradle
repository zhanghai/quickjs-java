import net.ltgt.gradle.errorprone.CheckSeverity

buildscript {
  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:4.2.1'
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:2.0.1'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.14.2'
  }
}

ext {
  compileSdkVersion = 29
  ndkAbiFilters = ['x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a']

  deps = [
      'androidx': [
          'annotation': 'androidx.annotation:annotation:1.1.0',
          'test': [
              'runner': 'androidx.test:runner:1.2.0',
          ],
      ],
      'duktape': 'com.squareup.duktape:duktape-android:1.4.0',
      'junit': 'junit:junit:4.13.2',
      'truth': 'com.google.truth:truth:1.0',
      'okio': 'com.squareup.okio:okio:1.14.1',
  ]
}

subprojects {
  repositories {
    mavenCentral()
    google()
    jcenter {
      // Required for a dependency of Android lint.
      content {
        includeGroup 'org.jetbrains.trove4j'
      }
    }
  }

  plugins.withId('com.android.library') {
    android.lintOptions {
      textReport true
      textOutput 'stdout'
      lintConfig rootProject.file('lint.xml')

      checkDependencies true
      checkTestSources false // TODO true https://issuetracker.google.com/issues/138247523
      explainIssues false

      // We run a full lint analysis as build part in CI, so skip vital checks for assemble task.
      checkReleaseBuilds false
    }
  }

  plugins.withId('com.vanniktech.maven.publish') {
    mavenPublish {
      releaseSigningEnabled = System.env.JITPACK != "true"
    }
    signing {
      def signingKey = findProperty('signingKey')
      def signingPassword = ''
      useInMemoryPgpKeys(signingKey, signingPassword)
    }
  }

  tasks.withType(Test).configureEach {
    testLogging {
      if (System.getenv('CI') == 'true') {
        events = ['failed', 'skipped', 'passed']
      }
      exceptionFormat 'full'
    }
  }

  apply plugin: 'net.ltgt.errorprone'

  dependencies {
    errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
    errorprone 'com.google.errorprone:error_prone_core:2.7.1'
  }

  tasks.withType(JavaCompile).configureEach {
    options.errorprone {
      check('MissingFail', CheckSeverity.ERROR)
      check('MissingOverride', CheckSeverity.ERROR)
      check('UnsafeFinalization', CheckSeverity.ERROR)
      check('UnusedException', CheckSeverity.ERROR)
      check('UnusedMethod', CheckSeverity.ERROR)
      check('UnusedNestedClass', CheckSeverity.ERROR)
      check('UnusedVariable', CheckSeverity.ERROR)
    }
  }
}
